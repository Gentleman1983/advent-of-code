import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    // Native SonarCloud support of Groovy is not currently available, so we have to wait for something to change or
    // miss that data.
    id 'groovy'
    id 'java-library'
}

dependencies {
    api project(":utils")

    api 'org.apache.groovy:groovy-all:4.0.17'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
        xml.outputLocation.set(file("${project.layout.buildDirectory}/jacoco/jacoco.xml"))
    }

    rootProject.tasks.sonar.dependsOn(it)
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
    }
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPath", "${project.layout.buildDirectory}/jacoco/jacoco.xml"
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = TestExceptionFormat.FULL
        events("passed", "failed", "skipped")
    }

    reports {
        junitXml.required = true
        html.required = true
    }

    jacocoTestReport.dependsOn(it)
}