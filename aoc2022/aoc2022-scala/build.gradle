import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'scala'
}

dependencies {
    implementation project(":utils")

    implementation 'org.scala-lang:scala3-library_3:3.3.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'org.scalatest:scalatest_3:3.3.0-SNAP4'
    testImplementation 'org.scalatestplus:junit-5-10_3:3.2.17.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
        xml.outputLocation.set(file("${project.layout.buildDirectory}/jacoco/jacoco.xml"))
    }

    rootProject.tasks.sonar.dependsOn(it)
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
    }
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPath", "${project.layout.buildDirectory}/jacoco/jacoco.xml"
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform {
        includeEngines("scalatest")
    }

    testLogging {
        exceptionFormat = TestExceptionFormat.FULL
        events("passed", "failed", "skipped")
    }

    reports {
        junitXml.required = true
        html.required = true
    }

    jacocoTestReport.dependsOn(it)
}
