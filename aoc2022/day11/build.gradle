import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'scala'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.scala-lang:scala3-library_3:3.3.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation 'org.scalatest:scalatest_3:3.3.0-SNAP4'
    testImplementation 'org.scalatestplus:junit-5-10_3:3.2.17.0'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.15.5'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform {
        includeEngines("junit-jupiter", "scalatest")
    }

    testLogging {
        exceptionFormat = TestExceptionFormat.FULL
        events("passed", "failed", "skipped")
    }

    reports {
        junitXml.required = true
        html.required = true
    }

    jacocoTestReport.dependsOn(it)
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
        xml.outputLocation.set(file("${buildDir}/jacoco/jacoco.xml"))
    }

    rootProject.tasks.sonar.dependsOn(it)
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
        attributes 'Main-Class': 'de.havox_design.aoc2022.day11.MainClass'
    }
}

application {
    mainClass = 'de.havox_design.aoc2022.day11.MainClass'
}
