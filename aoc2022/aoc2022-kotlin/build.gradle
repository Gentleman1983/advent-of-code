import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
}

dependencies {
    implementation project(":utils")

    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.15.5'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
        xml.outputLocation.set(file("${project.layout.buildDirectory}/jacoco/jacoco.xml"))
    }

    rootProject.tasks.sonar.dependsOn(it)
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
    }
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPath", "${project.layout.buildDirectory}/jacoco/jacoco.xml"
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = TestExceptionFormat.FULL
        events("passed", "failed", "skipped")
    }

    reports {
        junitXml.required = true
        html.required = true
    }

    jacocoTestReport.dependsOn(it)
}
