import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'org.sonarqube' version '4.4.1.3373'
}

// project meta data
group 'de.havox_design.aoc'
version '2023.27.4'

// Switch to gradle "all" distribution.
wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion '8.5'
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectKey", "de.havox_design.aoc:advent_of_code"
        property "sonar.organization", "havox"
        property "sonar.sourceEncoding", "UTF-8"
    }
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

allprojects {
    apply plugin: 'jacoco'
    apply plugin: 'jacoco-report-aggregation'
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
    }

    jacoco {
        toolVersion = '0.8.11'
    }

    jacocoTestReport {
        reports {
            html.required.set(true)
            xml.required.set(true)
        }

        rootProject.tasks.sonar.dependsOn(it)
    }

    sonarqube {
        properties {
            property "sonar.coverage.jacoco.xmlReportPath", "${project.layout.buildDirectory}/jacoco/jacoco.xml"
        }
    }

    tasks.named('check') {
        dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
    }

    tasks.withType(Test).configureEach {
        testLogging {
            exceptionFormat = TestExceptionFormat.FULL
            events("passed", "failed", "skipped")
        }

        reports {
            jacoco {
                //destinationFile = file( "${rootProject.layout.buildDirectory}/jacoco/combinedJacocoTest.exec" )
            }

            junitXml.required = true
            html.required = true
        }
    }
}
